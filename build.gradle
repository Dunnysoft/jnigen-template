buildscript {
    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.0.0-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "com.badlogicgames.gdx.gdx-jnigen"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    gdxVersion = '1.9.10'
}

version = '1.0.2-SNAPSHOT'

jnigen {
    sharedLibName = "demo"
    all {
        headerDirs = ["src"]

        cFlags += " -fvisibility=hidden "
        cppFlags += " -std=c++11 -fvisibility=hidden "
        linkerFlags += " -fvisibility=hidden "
    }
    add(Windows, x32)
    add(Windows, x64)
    add(Linux, x32)
    add(Linux, x64)
    add(Linux, x32, ARM)
    add(Linux, x64, ARM)
    add(MacOsX, x64)
    add(Android)
    add(IOS)
}

task runTest(dependsOn: jnigenJarNativesDesktop, type: JavaExec) {
    classpath = files(jnigenJarNativesDesktop.outputs)
    classpath += sourceSets.test.runtimeClasspath
    main = "demo.JnigenDemoTest"
    args jnigen.sharedLibName
}

dependencies {
    testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

apply from: 'publish.gradle'

apply plugin: 'maven-publish'

group = 'demo.jnigen'

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            
            artifact jnigenJarNativesWindows { }
            artifact jnigenJarNativesWindows64 { }
            artifact jnigenJarNativesLinux { }
            artifact jnigenJarNativesLinux64 { }
            artifact jnigenJarNativesLinuxARM { }
            artifact jnigenJarNativesLinuxARM64 { }
            
            ['arm64-v8a', 'armeabi-v7a', 'armeabi', 'x86_64', 'x86'].each { id ->
                artifact "jnigenJarNativesAndroid${id}" { }
            }
        }
        macos(MavenPublication) {
            artifact jnigenJarNativesMacOsX64 { }
            artifact jnigenJarNativesIOS { }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
        
        maven {
            name = "Github"
            url = "https://maven.pkg.github.com/PokeMMO/jnigen-template"
            
            credentials {
                //findProperty will allow these properties to be absent and still run the build, just won't publish
                username = findProperty("GITHUB_USERNAME")
                password = findProperty("GITHUB_API_TOKEN")
            }
        }
    }
}